apply plugin: 'com.android.application'
apply plugin: 'groovyx.android'
apply plugin: 'com.github.triplet.play'

android {
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "hackillinois-v${variant.versionName}.apk"
        }
    }

    playAccountConfigs {
        defaultAccountConfig {
            jsonFile = file('upload.json')
        }
    }

    compileSdkVersion 27
    defaultConfig {
        applicationId "org.hackillinois.androidapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 15
        versionName "1.1.10"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        playAccountConfig = playAccountConfigs.defaultAccountConfig
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'hackillinois'
            keyAlias 'hackillinois'
            keyPassword 'hackillinois'
        }
        if (file('upload.keystore').exists()) {
            upload {
                storeFile file('upload.keystore')
                storePassword System.getenv('HACKILLINOIS_UPLOAD_STORE_PASSWORD')
                keyAlias 'hackillinois'
                keyPassword System.getenv('HACKILLINOIS_UPLOAD_KEY_PASSWORD')
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            if (file('upload.keystore').exists()) {
                signingConfig signingConfigs.upload
            } else {
                signingConfig signingConfigs.debug
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        textReport true
        textOutput 'stdout'
        lintConfig rootProject.file('lint.xml')
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Android Official
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.android.support:appcompat-v7:27.0.2'
    implementation 'com.android.support:design:27.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:cardview-v7:27.0.2'

    // Debug libraries
    debugImplementation 'io.palaima.debugdrawer:debugdrawer:0.8.0'
    implementation 'io.palaima.debugdrawer:debugdrawer-commons:0.8.0'
    implementation 'io.palaima.debugdrawer:debugdrawer-okhttp3:0.8.0'
    debugImplementation 'com.readystatesoftware.chuck:library:1.1.0'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.1.0'
    implementation 'com.github.pedrovgs:lynx:1.1.0'
    implementation 'io.palaima.debugdrawer:debugdrawer-logs:0.8.0'
    implementation 'io.palaima.debugdrawer:debugdrawer-scalpel:0.8.0'
    implementation 'com.jakewharton.scalpel:scalpel:1.1.2'
    implementation 'io.palaima.debugdrawer:debugdrawer-timber:0.8.0'
    implementation 'io.palaima.debugdrawer:debugdrawer-actions:0.8.0'
    releaseImplementation 'io.palaima.debugdrawer:debugdrawer-no-op:0.8.0'
    debugImplementation 'com.balsikandar.android:crashreporter:1.0.9'

    // Other libraries used
    implementation 'com.dinuscxj:recyclerrefreshlayout:2.0.5'
    implementation 'com.evernote:android-job:1.2.4'
    implementation 'com.airbnb.android:lottie:2.5.0-beta3'
    implementation 'com.mikepenz:iconics-core:3.0.2@aar'
    implementation 'com.mikepenz:google-material-typeface:3.0.1.2.original@aar'
    implementation('com.fatboyindustrial.gson-jodatime-serialisers:gson-jodatime-serialisers:1.6.0') {
        exclude group: 'joda-time'
    }
    implementation 'net.danlew:android.joda:2.9.9.1'
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.10'
    implementation 'eu.davidea:flexible-adapter:5.0.0-rc4'
    implementation 'uk.co.chrisjenx:calligraphy:2.3.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'com.annimon:stream:1.1.9'
    implementation 'com.journeyapps:zxing-android-embedded:3.5.0'

    // test libraries
    // https://stackoverflow.com/questions/39186326/running-android-unit-tests-with-spock-framework
    testImplementation 'org.codehaus.groovy:groovy:2.4.12:grooid'
    testImplementation('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude group: 'org.codehaus.groovy'
        // exclude group: 'junit' <-- we don't have junit included earlier so we can comment this
    }
    testImplementation 'cglib:cglib-nodep:3.1'
    testImplementation 'org.objenesis:objenesis:2.5'
}

play {
    // ...
    track = 'beta'
    uploadImages = true
}